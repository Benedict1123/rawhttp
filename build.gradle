buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
    }
}

group 'com.athaydes.rawhttp'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

artifacts {
    archives javadocJar, sourcesJar
}

// add all the info required by Maven Central to the pom
configure(install.repositories.mavenInstaller) {
    pom.project {
        inceptionYear '2017'
        name project.name
        packaging 'jar'
        description project.description

        url 'https://github.com/renatoathaydes/rawhttp'

        scm {
            connection 'git@github.com:renatoathaydes/rawhttp.git'
            developerConnection 'git@github.com:renatoathaydes/rawhttp.git'
            url 'https://github.com/renatoathaydes/rawhttp'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'renatoathaydes'
                name 'Renato Athaydes'
                email 'renato@athaydes.com'
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty("bintrayUserName") ? bintrayUserName : null
    key = project.hasProperty("bintrayApiKey") ? bintrayApiKey : null
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'rawhttp'
        licenses = ['Apache-2.0']
        desc = project.description
        vcsUrl = 'https://github.com/renatoathaydes/rawhttp.git'
        websiteUrl = 'https://github.com/renatoathaydes/rawhttp'
        issueTrackerUrl = 'https://github.com/renatoathaydes/rawhttp/issues'
        labels = ['http', 'java', 'http-parsing']
        publicDownloadNumbers = true

        //noinspection GroovyAssignabilityCheck
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty("ossrhUsername") ? ossrhUsername : null
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : null
                close = '1' // '0' to NOT close
            }
        }
    }

}

bintrayUpload.dependsOn build, sourcesJar, fatJar